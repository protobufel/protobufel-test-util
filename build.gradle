buildscript {
    repositories {
        jcenter()
        mavenCentral()
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

plugins {
    id 'java-library'
    id 'maven'
    id 'signing'

    id 'io.codearte.nexus-staging' version "0.9.0"
    id 'net.researchgate.release' version '2.6.0'

    //id 'eclipse'
    id "me.champeau.gradle.jmh" version "0.4.2"

    id 'jacoco'
}

//plugins.withType(EclipsePlugin) {
//  project.eclipse.classpath.plusConfigurations += [ configurations.jmh ]
//}

group = 'com.github.protobufel'
archivesBaseName = 'protobufel-test-util'
sourceCompatibility = 1.8
targetCompatibility = 1.8

if (project.hasProperty('ossrhUsername')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'protobufel-test-util'
                    packaging 'jar'
                    description 'Common Test Utilities'
                    url 'https://github.com/protobufel/protobufel-test-util'

                    scm {
                        connection 'scm:git:https://github.com/protobufel/protobufel-test-util.git'
                        developerConnection 'scm:git@github.com:protobufel/protobufel-test-util.git'
                        url 'https://github.com/protobufel/protobufel-test-util'
                    }

                    licenses {
                        license {
                            name 'The BSD 3-Clause License'
                            url 'http://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'protobufel'
                            name 'David Tesler'
                            email 'protobufel@gmail.com'
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        //packageGroup = "com.github.protobufel.multikeymapjava" //optional if packageGroup == project.getGroup()
        stagingProfileId = "com.github.protobufel" //when not defined will be got from server using "packageGroup"
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task copyJavadocToDoc(type: Sync, group: 'documentation') {
        from javadoc
        into("docs/javadoc")
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    preTagCommit.dependsOn copyJavadocToDoc

    release {
        preCommitText = ''
        preTagCommitMessage = 'pre tag commit: '
        tagCommitMessage = 'bumped to '
        newVersionCommitMessage = 'new version commit: '
//        tagTemplate = "${version}"
    }

    afterReleaseBuild.dependsOn uploadArchives
} else {
    test {
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "short"

            debug {
                events "skipped", "failed"
                exceptionFormat "full"
            }

            info.events = ["failed", "skipped"]
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

repositories {
    jcenter()
    mavenCentral()
}

jmh {
    jmhVersion = '1.19' // Specifies JMH version
    includeTests = false
    // Allows to include test sources into generate JMH jar, i.e. use it when benchmarks depend on the test classes.
    duplicateClassesStrategy = 'FAIL'
    // Strategy to apply when encountring duplicate classes during creation of the fat jar (i.e. while executing jmhJar task)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }

        rule {
            enabled = false
            element = 'CLASS'
            includes = ['com.github.protobufel.*']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                maximum = 0.3
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

dependencies {
    api 'org.eclipse.jdt:org.eclipse.jdt.annotation:2.1.0'

    implementation group: 'com.google.protobuf', name: 'protobuf-java', version:'2.6.1'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.2'
    implementation group: 'junit', name: 'junit', version:'4.12'
    implementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    implementation 'org.mockito:mockito-core:2.8.47'
    implementation group: 'com.google.guava', name: 'guava', version:'18.0'
    implementation group: 'org.powermock', name: 'powermock-module-junit4', version:'1.5.6'
    implementation group: 'org.powermock', name: 'powermock-api-mockito', version:'1.5.6'
    implementation group: 'org.powermock', name: 'powermock-module-junit4-rule', version:'1.5.6'
    implementation group: 'org.powermock', name: 'powermock-classloading-xstream', version:'1.5.6'
    implementation group: 'org.assertj', name: 'assertj-core', version:'[1.7.0, 2.0.0)'
    implementation group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version:'[1.1.0, 2.0.0)'

    implementation 'com.github.protobufel:protobufel-protobuf-test-protos:0.6'
    implementation 'com.github.protobufel:protobufel-test-protos:0.6'

    // Use JUnit test framework
//    testImplementation 'com.github.protobufel:java-common-test:0.1.0'
//    testImplementation 'junit:junit:4.12'
//    testImplementation 'org.assertj:assertj-core:3.8.0'
//    testImplementation 'com.google.guava:guava:22.0'
//    testImplementation 'com.google.guava:guava-testlib:22.0'
//    testImplementation 'pl.pragmatists:JUnitParams:1.1.0'
}